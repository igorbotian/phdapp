//-------------------------------------------------------------------------------------------------
// Настройки, общие как для проекта в целом, так и для каждого его моделя по отдельности
//-------------------------------------------------------------------------------------------------

allprojects {
    apply plugin: 'java'
    group = phdAppGroup
    sourceCompatibility = phdAppSourceCompatibility
    version = phdAppVersion

    jar.archiveName = "${phdAppName}-${project.name}-${project.version}.jar"

    repositories {
        mavenCentral()
    }
}

//-------------------------------------------------------------------------------------------------
// Настройки модулей приложения
//-------------------------------------------------------------------------------------------------

project(':common') {
    description 'Domain area objects module'

    dependencies {
        compile(
                commonsLang,
                log4j,
                validationApi
        )

        testCompile junit
    }
}

project(':conf') {
    description 'Configuration interface module'

    dependencies {
        compile googleGuice
    }
}

project(':conf-properties') {
    description '.properties file-based configuration implementation module'

    dependencies {
        compile(
                project(':conf'),
                project(':ioc'),
                project(':utils'),
                gson,
                googleGuice,
                commonsLang,
                log4j
        )
    }
}

project(':input') {
    description 'Input data management interface module'

    dependencies {
        compile(
                project(':common'),
                project(':conf'),
                project(':utils'),
                commonsLang
        )

        testCompile(
                junit,
                easyMock
        )
    }
}

project(':input-json') {
    description 'JSON-based input data management implementation module'

    dependencies {
        compile(
                project(':input'),
                project(':ioc'),
                project(':conf'),
                gson,
                googleGuice,
                commonsLang,
                log4j
        )

        testCompile(
                junit,
                easyMock
        )
    }
}

project(':ioc') {
    description "Module providing some auxiliary features to implementation modules"

    dependencies {
        compile(
                googleGuice,
                googleGuiceMultibindings
        )
    }
}

project('locale') {
    description "Localization interface module"
}

project(':locale-java-i18n') {
    description "Java I18N-based localization implementation module"

    dependencies {
        compile(
                project(':ioc'),
                project(':locale'),
                googleGuice,
                commonsLang,
                log4j
        )

        testCompile junit
    }
}

project(':log-log4j') {
    description 'Log4j logging-related facilities module'

    dependencies {
        compile log4j
    }
}

project(':main') {
    description 'Main module. Application entry point'

    dependencies {
        compile(
                project(':conf-properties'),
                project(':input-json'),
                project(':locale-java-i18n'),
                project(':log-log4j'),
                project(':utils')
        )
    }
}

project(':svm') {
    description 'SVM classification mechanism'

    dependencies {
        compile(
                project(':common')
        )

        testCompile junit
    }
}

project(':svm-test') {
    description 'Testing facilities for the new SVM classification mechanism'

    dependencies {
        compile(
                project(':common'),
                project(':conf'),
                project(':input')
        )

        testCompile junit
    }
}

project(':utils') {
    description 'An utilitarian module'

    dependencies {
        compile(
                project(':ioc'),
                log4j
        )
    }
}

dependencies {
    compile project(':main')
}

//-------------------------------------------------------------------------------------------------
// Конфигурация дистрибутива приложения
//-------------------------------------------------------------------------------------------------

apply plugin: 'application'

mainClassName = phdAppMainClassName
applicationDefaultJvmArgs = ["-Dphdapp.conf.folder=../conf"]

startScripts {
    classpath += files('src/dist/lib/conf')
}

distZip {
    baseName = phdAppName
}