//-------------------------------------------------------------------------------------------------
// Настройки, общие как для проекта в целом, так и для каждого его моделя по отдельности
//-------------------------------------------------------------------------------------------------

allprojects {
    apply plugin: 'java'
    group = phdAppGroup
    sourceCompatibility = phdAppSourceCompatibility
    version = phdAppVersion

    jar.archiveName = "${phdAppName}-${project.name}-${project.version}.jar"

    repositories {
        mavenCentral()
    }
}

//-------------------------------------------------------------------------------------------------
// Настройки модулей приложения
//-------------------------------------------------------------------------------------------------

project(':common') {
    description 'Domain area objects module'

    dependencies {
        compile(
                commonsLang,
                log4j,
                validationApi
        )

        testCompile junit
    }
}

project(':conf') {
    description 'Configuration interface module'

    dependencies {
        compile googleGuice
    }
}

project(':conf-properties') {
    description '.properties file-based configuration implementation module'

    dependencies {
        compile(
                project(':conf'),
                gson,
                googleGuice,
                commonsLang,
                log4j
        )
    }
}

project(':input') {
    description 'Input data management interface module'

    dependencies {
        compile(
                project(':common'),
                project(':conf'),
                commonsLang,
                commonsIO
        )

        testCompile(
                junit,
                easyMock,
                commonsIO
        )
    }
}

project(':input-json') {
    description 'JSON-based input data management implementation module'

    dependencies {
        compile(
                project(':input'),
                project(':conf'),
                gson,
                googleGuice,
                commonsLang,
                log4j
        )

        testCompile(
                junit,
                easyMock
        )
    }
}

project(':log-log4j') {
    description 'Log4j logging-related facilities module'

    dependencies {
        compile log4j
    }
}

project(':main') {
    dependencies {
        compile(
                project(':conf'),
                project(':conf-properties'),
                project(':input'),
                project(':input-json'),
                project(':log-log4j')
        )
    }
}

dependencies {
    compile project(':main')
}

//-------------------------------------------------------------------------------------------------
// Конфигурация дистрибутива приложения
//-------------------------------------------------------------------------------------------------

apply plugin: 'application'

mainClassName = phdAppMainClassName
applicationDefaultJvmArgs = ["-Dphdapp.conf.folder=../conf"]

startScripts {
    classpath += files('src/dist/lib/conf')
}

distZip {
    baseName = phdAppName
}